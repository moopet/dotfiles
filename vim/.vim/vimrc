"{{{ Plugins

"{{{ Auto-install vim-plug
if empty(glob('~/.vim/autoload/plug.vim'))
  let vimdir = expand("~/.vim")

  if !isdirectory(vimdir)
    call mkdir(vimdir)
  endif

  let autoloaddir = expand("~/.vim/autoload")

  if !isdirectory(autoloaddir)
    call mkdir(autoloaddir)
  endif

  silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim

  autocmd VimEnter * PlugInstall
endif
"}}}

call plug#begin('~/.vim/plugged')

Plug 'airblade/vim-gitgutter'
" Plug 'christoomey/vim-tmux-navigator'
Plug 'freitass/todo.txt-vim'
Plug 'RRethy/vim-hexokinase', { 'do': 'make hexokinase' }
Plug 'itchyny/lightline.vim'
Plug 'RRethy/vim-illuminate'
Plug 'kshenoy/vim-signature'
Plug 'mhinz/vim-startify'
Plug 'mustache/vim-mustache-handlebars'
Plug 'lumiliet/vim-twig'
Plug 'w0rp/ale'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-vinegar'
Plug 'Yggdroot/indentLine'

if v:version > 802 || has("patch1593") || has('nvim')
  Plug 'Lenovsky/nuake'
else
  command Nuake :echo "Nuake needs vim > 802 with patch 1593 or neovim to run."
endif

Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
Plug 'junegunn/fzf.vim'

" Plug 'neoclide/coc.nvim', {'branch': 'release'}

"{{{ Colorschemes
Plug 'morhetz/gruvbox'
"}}}

"{{{ Currently unused but good to keep as bookmarked

"Plug 'chrisbra/vim-diff-enhanced'
"Plug 'evanmiller/nginx-vim-syntax'
"Plug 'joonty/vdebug'
"Plug 'mbbill/undotree'
"Plug 'tpope/vim-surround'
"Plug 'Valloric/MatchTagAlways'
" Plug 'tpope/vim-commentary'
"}}}

call plug#end()
"}}}

"{{{ Common settings
set nomodeline

let mapleader = "\<Space>"
let maplocalleader = "\\"

set termguicolors
let g:Hexokinase_highlighters = [ 'backgroundfull' ]
let g:solarized_termcolors = 256
let g:loaded_getscriptPlugin = 1
let g:loaded_2html_plugin = 1
let g:loaded_vimballPlugin = 1
let g:loaded_rrhelper = 1
let g:gitgutter_max_signs = 1000
let g:SignatureMarkTextHLDynamic = 1

" examples of good indentLine_chars: ¬¶ ‚îÜ ‚îÇ ‚é∏ ‚ñè
let g:indentLine_char = '‚îä'
let g:indentLine_enabled = 0

syntax on
runtime macros/matchit.vim

augroup numbertoggle
  autocmd!
  autocmd BufEnter,FocusGained,InsertLeave * set relativenumber
  autocmd BufLeave,FocusLost,InsertEnter   * set norelativenumber
augroup END

" Searching
set hlsearch
set ignorecase
set incsearch
set showmatch
set smartcase
nnoremap / /\v
xnoremap / /\v

command Todo :execute 'vimgrep todo '.expand('%') | :copen | :cc
nnoremap // :Rg <c-r><c-w><cr>
" These mappings are similar to Unimpaired but not quite.
nnoremap [q :cprevious<cr>*<c-o>zz
nnoremap ]q :cnext<cr>*<c-o>zz
nnoremap [Q :cfirst<cr>*<c-o>zz
nnoremap ]Q :clast<cr>*<c-o>zz

" Layout and indenting
set autoindent
" Override cursorline later so it's only there on active windows.
set nocursorline
set expandtab
set formatoptions=tcqrn1j
set scrolloff=2
set shiftround
set shiftwidth=2
set softtabstop=2
set tabstop=2
set textwidth=80

set dictionary+=/usr/share/dict/words
set diffopt+=iwhite
set encoding=utf-8
set fillchars=diff:‚£ø
set hidden
" set lazyredraw
set listchars=tab:‚ñ∏\ ,eol:¬¨,extends:‚ùØ,precedes:‚ùÆ
set list
set showbreak="‚Ü™\ "
set splitbelow
set splitright

set autoread
set autowrite
set backspace=indent,eol,start

set laststatus=2
set linebreak
set nolist
set number
set ruler
set title
set ttyfast
set updatetime=100

set timeout timeoutlen=3000 ttimeoutlen=100

" matching
set wildignore+=.hg,.git,.svn,*.orig
set wildignore+=.DS_Store,thumbs.db,__MACOSX
set wildignore+=*.jpg,*.bmp,*.gif,*.png,*.jpeg,*.ico
set wildignore+=*.flv,*.mp4,*.webm,*.ogv,*.ogg,*.mp3,*.fla,*.swf
set wildignore+=*.o,*.obj,*.exe,*.dll,*.manifest,*.pyc,*.lib,*.so
set wildignore+=*.sw?
set wildignore+=*.bak,*.out,*.old,*.oot,*.ffs,*.wtf
set wildignore+=.o,.info,.aux,.idx,.out,.toc
set wildignore+=*.zip,*.rar,*.r00,*.gz,*.bat
set wildignore+=migrations,fixtures

set wildignorecase
set wildmenu
" sane (bash-like) tab expansion
set wildmode=longest,list

function! ToggleWhitespaceHints()
  set list!

  if &list
    set colorcolumn=+1
  else
    set colorcolumn=
  endif
endfunction

inoremap <F2> <esc>:call ToggleWhitespaceHints()<CR>i
nnoremap <F2> :call ToggleWhitespaceHints()<CR>

inoremap <F3> <esc>:set paste!<CR>i
nnoremap <F3> :set paste!<CR>

nnoremap <F4> :IndentLinesToggle<CR>

let g:nuake_position = 'bottom'
nnoremap <F1> :Nuake<CR>
inoremap <F1> <C-\><C-n>:Nuake<CR>
tnoremap <F1> <C-\><C-n>:Nuake<CR>

" Completion
set completeopt=longest,menuone,preview

autocmd BufEnter * call MyLastWindow()
function! MyLastWindow()
  " if the window is quickfix go on
  if &buftype=="quickfix" || &buftype=="nofile"
    " if this window is last on screen quit without warning
    if winnr('$') < 2
      quit!
    endif
  endif
endfunction

" if we've opened a file and don't have the required root permissions...
cnoremap w!! execute 'silent! write !sudo tee % >/dev/null' <bar> edit!
"}}}

"{{{ GVim settings
if has('gui_running')
  set guioptions=e
  set guifont=Monospace\ 9
endif
"}}}

"{{{ Mac fixes
" Macs are dumb and crontab -e fails with "temp file must be edited in place"
set backupskip=/tmp/*,/private/tmp/*
"}}}

"{{{ Fuzzy finder
function! s:fzf_root()
	let path = finddir(".git", expand("%:p:h").";")
	return fnamemodify(substitute(path, ".git", "", ""), ":p:h")
endfunction

" Command for git grep
" - fzf#vim#grep(command, with_column, [options], [fullscreen])
command! -bang -nargs=* GGrep
  \ call fzf#vim#grep(
  \   'git grep --line-number '.shellescape(<q-args>), 0,
  \   { 'dir': systemlist('git rev-parse --show-toplevel')[0] }, <bang>0)

" Override Colors command. You can safely do this in your .vimrc as fzf.vim
" will not override existing commands.
command! -bang Colors
  \ call fzf#vim#colors({'options': '--reverse --margin 30%,0'}, <bang>0)

" Augmenting Ag command using fzf#vim#with_preview function
"   * fzf#vim#with_preview([[options], [preview window], [toggle keys...]])
"     * For syntax-highlighting, Ruby and any of the following tools are required:
"       - Bat: https://github.com/sharkdp/bat
"       - Highlight: http://www.andre-simon.de/doku/highlight/en/highlight.php
"       - CodeRay: http://coderay.rubychan.de/
"       - Rouge: https://github.com/jneen/rouge
"
"   :Ag  - Start fzf with hidden preview window that can be enabled with "?" key
"   :Ag! - Start fzf in fullscreen and display the preview window above
command! -bang -nargs=* Ag
  \ call fzf#vim#ag(<q-args>,
  \                 <bang>0 ? fzf#vim#with_preview('up:60%')
  \                         : fzf#vim#with_preview('right:50%:hidden', '?'),
  \                 <bang>0)

" Similarly, we can apply it to fzf#vim#grep. To use ripgrep instead of ag:
command! -bang -nargs=* Rg
  \ call fzf#vim#grep(
  \   'rg --column --line-number --no-heading --color=always --smart-case '.shellescape(<q-args>), 1,
  \   <bang>0 ? fzf#vim#with_preview({'options': '--preview "sort" --delimiter : --nth 4..'}, 'up:60%:hidden')
  \           : fzf#vim#with_preview({'options': '--preview "sort" --delimiter : --nth 4..'}, 'right:50%', '?'),
  \   <bang>0)

" Likewise, Files command with preview window
command! -bang -nargs=? -complete=dir Files
  \ call fzf#vim#files(<q-args>, fzf#vim#with_preview(), <bang>0)
nnoremap <c-p> :exe 'Files ' . <SID>fzf_root()<cr>

" This nonsense right here, "œÄ", is alt-p on a Mac.
nnoremap œÄ :Rg<cr>
"}}}

"{{{ Autocomplete
set omnifunc=syntaxcomplete#Complete
"}}}

" Quick way to remember to paste the last yanked item
nnoremap yp "0p

" On the mac I kind of hate scroll shifiting the whole terminal, so...
set mouse=a

" Keep visual selection while indenting.
xnoremap < <gv
xnoremap > >gv

" Add more robustness to <c-l>
" https://github.com/mhinz/vim-galore#saner-ctrl-l
nnoremap <c-l> :nohlsearch<cr>:diffupdate<cr>:syntax sync fromstart<cr><c-l>

" Unipoo.
iabbr :poo: üí©
iabbr :tm: ‚Ñ¢

"{{{ MacOS
" Solve the clipboard issue on shit terminals.
augroup mac_os
  autocmd!
  if executable('rg')
    let tmpdir = expand("~/.vim/tmp")
    if !isdirectory(tmpdir)
      call mkdir(tmpdir)
    endif

    xnoremap <leader>y myy:'<,'>w!~/.vim/tmp/vim-clipboard<cr>:silent !cat ~/.vim/tmp/vim-clipboard \| pbcopy<cr>:redraw!<cr>`y
    xnoremap <leader>p :silent r!pbpaste<cr>
    nnoremap <leader>Y y:silent! w!~/.vim/tmp/vim-clipboard<cr>:silent !cat ~/.vim/tmp/vim-clipboard \| pbcopy<cr>:redraw!<cr>
    nnoremap <leader>yy y:silent! w!~/.vim/tmp/vim-clipboard<cr>:silent !cat ~/.vim/tmp/vim-clipboard \| pbcopy<cr>:redraw!<cr>
    nnoremap <leader>p :silent r!pbpaste<cr>
  endif
augroup END

"{{{ Grep
" I don't know how to make ag exclude junk files yet, so this takes ages to run
" if there are minified files or SQL dumps lying around. I thought it understood
" the same arguments as ack, but it seems not...
if executable('rg')
  let g:ackprg = 'rg --column'
  set grepprg=rg\ --vimgrep
  set grepformat=%f:%l:%c:%m
elseif executable('ag')
  let g:ackprg = 'ag --nogroup --nocolor --column --depth 10 --ignore-dir=var --ignore-dir=nbproject --ignore-dir=cache --ignore-dir=migrations --ignore-dir=webroot/static --ignore-dir=vendor'
  set grepprg=ag\ --vimgrep
  set grepformat=%f:%l:%c:%m
elseif executable('ack')
  let g:ackprg = 'ack --nogroup --nocolor --smart-case --type-add=php=.tpl,.inc,.module,.install --type-set=csv=.csv --type-set=ini=.ini,.cfg,.rc --type-set=min=.min.js --type-set=vim=.vim --type-set=log=.log --type-set=test=.test --type-set=sass=.sass,.scss --type-set=scss=.sass,.scss --nomin --notest --nocsv --nosql --ignore-dir=var --ignore-dir=nbproject --ignore-dir=cache --ignore-dir=migrations --ignore-dir=webroot/static --ignore-dir=vendor'
endif
"}}}

" Persistent undo                                                           
if has('persistent_undo')
  let undodir = expand("~/.vim/undo")
  if !isdirectory(undodir)
    call mkdir(undodir)
  endif
  set undodir=~/.vim/undo/
  set undofile
endif

let backupdir = expand("~/.vim/backups")
if !isdirectory(backupdir)
  call mkdir(backupdir)
endif
set backupdir=~/.vim/backups/
set backup
set noswapfile

" Resize splits when the window is resized
augroup neat_resizing
  autocmd!
  autocmd VimResized * exe "normal! \<c-w>="
augroup END

" Make sure Vim returns to the same line when you reopen a file.
" Thanks, Amit
augroup line_return
  autocmd!
  autocmd BufReadPost *
        \ if line("'\"") > 0 && line("'\"") <= line("$") |
        \     execute 'normal! g`"zvzz' |
        \ endif
augroup END

" Split line (sister to [J]oin lines)
" The normal use of S is covered by cc, so don't worry about shadowing it.
nnoremap S i<cr><esc><right>

" Emacs bindings
cnoremap <c-a> <home>
cnoremap <c-e> <end>
inoremap <c-a> <esc>I
inoremap <c-e> <esc>A

"{{{ Lightline
set noshowmode
"}}}

"{{{ Colorschemes
" Regardless of the colorscheme I'm running, I always want certain things
function! CustomColorscheme()
  set background=dark
  let g:gruvbox_contrast_dark = 'hard'
  silent! colorscheme gruvbox

  highlight! link Exception Keyword

  if has("&colorcolumn")
    set colorcolumn=+1
    call matchadd('ColorColumn', '\%81v', 100)
    highlight ColorColumn ctermbg=52
  endif

  " Highlight VCS conflict markers
  match ErrorMsg '^\(<\|=\|>\)\{7\}\([^=].\+\)\?$'

  " Lets us use italics.
  set t_ZH=[3m
  set t_ZR=[23m

  " Customise things so there are nice transparent backgrounds.
  highlight IlluminatedWord ctermfg=214 ctermbg=237 
  " highlight Search cterm=NONE ctermbg=yellow ctermfg=black
  highlight Normal ctermbg=235
  " ColorColumn is used by the dim-inactive plugin to change background colors.
  highlight ColorColumn ctermbg=236
  highlight NonText ctermbg=NONE
  highlight LineNr ctermbg=NONE
  highlight Visual cterm=NONE ctermbg=162 ctermfg=white
  highlight CursorLine ctermbg=23
  highlight SignColumn ctermbg=232
  highlight GitGutterAdd ctermbg=232
  highlight GitGutterChange ctermbg=232
  highlight GitGutterDelete ctermbg=232
  highlight SignatureMarkText ctermbg=232 ctermfg=14
  highlight Comment cterm=italic
  highlight Folded ctermbg=black
  highlight SpellBad cterm=underline,italic ctermfg=darkred
endfunction

function! ToggleBackground()
  if &background == 'dark'
    set background=light
  else
    set background=dark
    call CustomColorscheme()
  endif
endfunction

" Show syntax highlighting groups for word under cursor
nnoremap <leader>ss :call <SID>SynStack()<CR>
function! <SID>SynStack()
  if !exists("*synstack")
    return
  endif
  echo map(synstack(line('.'), col('.')), 'synIDattr(v:val, "name")')
endfunction

nnoremap <f5> :call ToggleBackground()<cr>
nnoremap <f6> :IlluminationToggle<cr>

call CustomColorscheme()

augroup ActiveWindowHighlight
  autocmd!
  autocmd WinEnter * set cursorline
  autocmd WinLeave * set nocursorline
augroup END
"}}}

nnoremap <silent> <BS> :nohlsearch<CR>

" Keep search matches in the middle of the window and pulse the line when moving
" to them.
nnoremap n nzzzv
nnoremap N Nzzzv

" Don't move on * or #
nnoremap # #<c-o>
nnoremap * *<c-o>

" Same when jumping around
nnoremap g; g;zz
nnoremap g, g,zz

nnoremap <leader>u :UndotreeToggle<CR>

" select the last changed (or pasted) text, and the visual mode will be the same
" as was last used http://vim.wikia.com/wiki/Selecting_your_pasted_text
nnoremap <expr> gp '`[' . strpart(getregtype(), 0, 1) . '`]'

"{{{ Django
augroup ft_django
  autocmd!

  autocmd BufNewFile,BufRead urls.py           setlocal nowrap
  autocmd BufNewFile,BufRead urls.py           normal! zR
  autocmd BufNewFile,BufRead dashboard.py      normal! zR
  autocmd BufNewFile,BufRead local_settings.py normal! zR

  autocmd BufNewFile,BufRead admin.py     setlocal filetype=python.django
  autocmd BufNewFile,BufRead urls.py      setlocal filetype=python.django
  autocmd BufNewFile,BufRead models.py    setlocal filetype=python.django
  autocmd BufNewFile,BufRead views.py     setlocal filetype=python.django
  autocmd BufNewFile,BufRead *_tags.py    setlocal filetype=python.django
  autocmd BufNewFile,BufRead functions.py setlocal filetype=python.django
  autocmd BufNewFile,BufRead settings.py  setlocal filetype=python.django
  autocmd BufNewFile,BufRead settings.py  setlocal foldmethod=marker
  autocmd BufNewFile,BufRead forms.py     setlocal filetype=python.django
augroup END
"}}}

augroup ft_css
  autocmd!

  xnoremap <leader>i :s/\v\s+!important;/;/g<cr>gv:s/\v;/ !important;/g<cr>:nohl<cr>
  xnoremap <leader>I :s/\v\s+\!important;/;/g<cr>:nohl<cr>

  autocmd BufNewFile,BufRead *.less setlocal filetype=less
  autocmd BufNewFile,BufRead *.scss setlocal filetype=scss

  autocmd Filetype scss,less,css setlocal foldmarker={,}
  autocmd Filetype scss,less,css setlocal omnifunc=csscomplete#CompleteCSS
  autocmd Filetype scss,less,css setlocal iskeyword+=-

  " Use <leader>S to sort properties.  Turns this:
  "
  "     p {
  "         width: 200px;
  "         height: 100px;
  "         background: red;
  "
  "         ...
  "     }
  "
  " into this:

  "     p {
  "         background: red;
  "         height: 100px;
  "         width: 200px;
  "
  "         ...
  "     }
  autocmd BufNewFile,BufRead *,scss,*.less,*.css nnoremap <buffer> <leader>S ?{<CR>jV/\v^\s*\}?$<CR>k:sort<CR>:noh<CR>

  " Make {<cr> insert a pair of brackets in such a way that the cursor is correctly
  " positioned inside of them AND the following code doesn't get unfolded.
  autocmd BufNewFile,BufRead *.scssm*.less,*.css inoremap <buffer> {<cr> {}<left><cr><space><space><space><space>.<cr><esc>kA<bs>
augroup END

"{{{ Quickfix window
augroup ft_quickfix
  autocmd!
  autocmd Filetype qf setlocal colorcolumn=0 nolist nocursorline nowrap
augroup END
"}}}

"{{{ Vim
augroup ft_vim
  autocmd!

  autocmd FileType vim setlocal foldmethod=marker
  autocmd FileType vim setlocal foldlevel=0
  autocmd FileType help setlocal textwidth=78
  autocmd BufWinEnter *.txt if &ft == 'help' | wincmd L | endif
augroup END
"}}}

"{{{ Git
function! PrepopulateGitCommitMessage()
  if search("On branch feature")
    execute "normal! /On branch f/ly$ggP^2f-C "
    startinsert!
  endif
endfunction

augroup git
  autocmd!

  autocmd Filetype gitcommit call PrepopulateGitCommitMessage()
  autocmd BufNewFile,BufRead .git/index setlocal nolist
augroup END
"}}}

"{{{ Nginx
augroup ft_nginx
  autocmd!

  autocmd BufRead,BufNewFile /etc/nginx/conf/*                      set ft=nginx
  autocmd BufRead,BufNewFile /etc/nginx/sites-available/*           set ft=nginx
  autocmd BufRead,BufNewFile /usr/local/etc/nginx/sites-available/* set ft=nginx
  autocmd BufRead,BufNewFile vhost.nginx                            set ft=nginx

  autocmd FileType nginx setlocal foldmethod=marker foldmarker={,}
augroup END
"}}}

" Motion for "next/last object". For example, "din(" would go to the next "()"
" pair and delete its contents.

onoremap an :<c-u>call <SID>NextTextObject('a', 'f')<cr>
xnoremap an :<c-u>call <SID>NextTextObject('a', 'f')<cr>
onoremap in :<c-u>call <SID>NextTextObject('i', 'f')<cr>
xnoremap in :<c-u>call <SID>NextTextObject('i', 'f')<cr>

onoremap al :<c-u>call <SID>NextTextObject('a', 'F')<cr>
xnoremap al :<c-u>call <SID>NextTextObject('a', 'F')<cr>
onoremap il :<c-u>call <SID>NextTextObject('i', 'F')<cr>
xnoremap il :<c-u>call <SID>NextTextObject('i', 'F')<cr>

function! s:NextTextObject(motion, dir)
  let c = nr2char(getchar())

  if c ==# "b"
    let c = "("
  elseif c ==# "B"
    let c = "{"
  elseif c ==# "d"
    let c = "["
  endif

  exe "normal! ".a:dir.c."v".a:motion.c
endfunction

" I don't like having :w etc. scattered in non-saved files
" and for some reason my setup fucks up and sometimes ignores <esc>
inoremap :w<cr> <esc>:w<cr>
inoremap :wq<cr> <esc>:wq<cr>
inoremap :x<cr> <esc>:x<cr>

" Open a Quickfix window for the last search.
nnoremap <silent> <leader>/ :execute 'vimgrep /'.@/.'/g %'<CR>:copen<CR>

" Fix linewise visual selection of various text objects
nnoremap VV V
nnoremap Vit vitVkoj
nnoremap Vat vatV
nnoremap Vab vabV
nnoremap VaB vaBV

" Map j and k to gj/gk, but only when no count is given
" However, for larger jumps like 6j add the current position to the jump list
" so that you can use <c-o>/<c-i> to jump to the previous position
nnoremap <expr> j v:count ? (v:count > 5 ? "m'" . v:count : '') . 'j' : 'gj'
nnoremap <expr> k v:count ? (v:count > 5 ? "m'" . v:count : '') . 'k' : 'gk'

nnoremap <leader>W :%s/\s\+$//<cr>:let @/=''<CR>

"{{{ CtrlP
let g:ctrlp_working_path_mode = 'rw'
let g:ctrlp_root_markers = 'all'
let g:ctrlp_clear_cache_on_exit = 0
let g:ctrlp_cache_dir = $HOME.'/.vim/tmp/ctrlp'
"}}}

"{{{ Drupal
augroup drupal
  autocmd!

  autocmd BufNewFile *.info setlocal ft=info
  autocmd BufNewFile *.install,*.module setlocal ft=php
augroup END
"}}}

"{{{ HTML
augroup ft_html
  autocmd!

  autocmd FileType html,htmldjango set filetype=html.mustache
  autocmd FileType html setlocal indentkeys-=*<Return>
  autocmd BufNewFile,BufRead *.html setlocal filetype=htmldjango
  autocmd FileType html,jinja,htmldjango setlocal foldmethod=manual

  " Use <localleader>f to fold the current tag.
  autocmd FileType html,jinja,htmldjango nnoremap <buffer> <localleader>f Vatzf

  " Use Shift-Return to turn this:
  "     <tag>|</tag>
  "
  " into this:
  "     <tag>
  "         |
  "     </tag>
  autocmd FileType html,jinja,htmldjango nnoremap <buffer> <s-cr> vit<esc>a<cr><esc>vito<esc>i<cr><esc>

  " Indent tag
  autocmd FileType html,jinja,htmldjango nnoremap <buffer> <localleader>= Vat=

  " Django tags
  autocmd FileType jinja,htmldjango inoremap <buffer> <c-t> {%<space><space>%}<left><left><left>

  " Django variables
  autocmd FileType jinja,htmldjango inoremap <buffer> <c-f> {{<space><space>}}<left><left><left>

  autocmd FileType htmldjango setlocal commentstring={#\ %s\ #}
augroup END
"}}}

"{{{ i3
augroup i3
  autocmd BufWritePost ~/.config/i3/config call system('which i3-msg && i3-msg reload')
augroup END
"}}}

"{{{ Javascript
  autocmd FileType javascript nnoremap <buffer> <leader>cl mzyiWoconsole.log('TCL: ', <esc>pa);<esc>`z
" Simple re-format for minified Javascript
command! UnMinify call UnMinify()
function! UnMinify()
  %s/{\ze[^\r\n]/{\r/g
  %s/){/) {/g
  %s/};\?\ze[^\r\n]/\0\r/g
  %s/;\ze[^\r\n]/;\r/g
  %s/[^\s]\zs[=&|]\+\ze[^\s]/ \0 /g
  normal ggVG=
endfunction
"}}}

"{{{ Big files
augroup sql
  autocmd!
  autocmd Filetype xml if getfsize(@%) > 1000000 | setlocal syntax=OFF | setlocal nowrap | endif
  autocmd Filetype sql if getfsize(@%) > 1000000 | setlocal syntax=OFF | setlocal nowrap | endif
"}}}

"{{{ PHP
augroup php
	autocmd!

  function! ShowFuncName()
    echohl ModeMsg
    echo substitute(getline(search("function", "bcnWz")), '^\s*\|\s*{.*$', '', 'g')
    echohl None
  endfunction
  nnoremap <leader>? :call ShowFuncName()<CR>
  nnoremap <leader>= ggqz/ array(<cr>mzf(%r]`zcf([<esc>q9999@q

  nnoremap n nzzzv:call ShowFuncName()<CR>
  nnoremap N Nzzzv:call ShowFuncName()<CR>


  " This doesn't work for gx, because for some reason <cword> doesn't get expanded...
  " autocmd FileType php let g:netrw_gx="https://www.php.net/search.php?show=quickref&pattern=<cword>"
  " so instead...
  nnoremap gx :call PhpManual()<cr>

  function! PhpManual()
      let url = "https://www.php.net/manual-lookup.php?scope=quickref&pattern=" . expand("<cword>")
      exec 'silent !open "' . url . '"'
      redraw!
  endfunction

  nnoremap <leader>zt :call search('^\s*fu\%[nction]\>', "bW")<cr>zt
  nnoremap <leader>r ggqz/array\s*\(<cr>mzf(%r]`zcf([q9999@q
	autocmd BufNewFile,BufFilePre,BufRead *.install set filetype=php
	autocmd BufNewFile,BufFilePre,BufRead *.test set filetype=php
	autocmd FileType php nnoremap <buffer> <leader>cc :!drush cc all
	autocmd FileType php nnoremap <buffer> <leader>f ?}o/**/function () {}kf(:nohl<cr>i
  autocmd FileType php nnoremap <buffer> <leader>dump Oheader('X-XSS-Protection:0');ob_get_clean(); echo '<pre>'; var_dump(); echo '</pre>'; die(__FILE__ . ':' . __LINE__);<esc>2F(a
  autocmd FileType php nnoremap <buffer> <leader>back Oheader('X-XSS-Protection:0');ob_get_clean(); echo '<pre>'; foreach (debug_backtrace() as $d) { echo $d['file'] . ':' . $d['line'] . ' ' . $d['function'] . "()\n"; } echo '</pre>'; die(__FILE__ . ':' . __LINE__);<esc>^
  autocmd FileType php nnoremap <buffer> <leader>dog Owatchdog('devel', '', [var_export(, 1)], WATCHDOG_DEBUG);<esc>F(a
  autocmd FileType php nnoremap <buffer> <leader>sdd Osignal_debug_dump();<esc>F(a

  autocmd FileType php nnoremap <buffer> gd :cclose<cr>:set nohlsearch<cr>/function <c-r><c-w><cr>:set hlsearch<cr>w*<c-o>zz
  autocmd FileType php nnoremap <buffer> gD *:Rg function.<c-r><c-w><cr>zt

  autocmd FileType php set nofoldenable
  " autocmd FileType php cnoremap drush !drush scr %

  " Dirty text object for PHP functions
  autocmd FileType php,javascript nnoremap <buffer> gf mz$?^\s*function<CR>:noh<CR>w"zyiw`z:echo @z<cr>
  autocmd FileType php,javascript nnoremap <buffer> gnf mz$/^\s*function<CR>:noh<CR>w"zyiw`z:echo @z<cr>
augroup END
"}}}

"{{{ Python
function! DeleteTrailingWS()
  exe "normal mz"
  %s/\s\+$//ge
  exe "normal `z"
endfunction

augroup ft_python
  let g:pymode_folding = 0
  let g:pymode_lint_ignore = "E501,E302,E303"
  let g:pymode_lint_onfly = 1
  let g:pymode_rope = 0
  autocmd!

  " autocmd FileType python setlocal omnifunc=pythoncomplete#Complete
  autocmd FileType python setlocal define=^\s*\\(def\\\\|class\\)

  "au FileType python compiler nose
  autocmd FileType man nnoremap <buffer> <cr> :q<cr>

  " Jesus tapdancing Christ, built-in Python syntax, you couldn't let me
  " override this in a normal way, could you?
  autocmd FileType python if exists("python_space_error_highlight") | unlet python_space_error_highlight | endif

  " Jesus, Python.  Five characters of punctuation for a damn string?
  autocmd FileType python inoremap <buffer> <d-'> _(u'')<left><left>

  autocmd FileType python nnoremap <f5> :execute '!ipdb3 -c "break ' . line(".") . '" -c "continue" %'<cr>

  autocmd bufwrite *.py :call DeleteTrailingWS()
augroup END
"}}}

"{{{ Startify
" Set startify like a superpowered vinegar (shift -)
nnoremap _ :Startify<cr>

highlight StartifyNumber ctermfg=220
highlight StartifyBracket ctermfg=804
highlight StartifyFile ctermfg=003

let g:startify_custom_header = []
let g:startify_bookmarks = [
      \ { 'a': '~/.aliases' },
      \ { 'b': '~/.bashrc' },
      \ { 'i': '~/.config/i3/config' },
      \ { 'g': '~/.config/git/config' },
      \ { 'r': '~/.config/ranger/rc.conf' },
      \ { 'o': '~/.oh-my-zsh/custom/custom.zsh' },
      \ { 'p': '~/.config/polybar/config' },
      \ { 't': '~/.tmux.conf' },
      \ { 'x': '~/.Xresources' },
      \ { 'v': '~/.vim/vimrc' },
      \ { 'z': '~/.zshrc' },
      \ ]

let g:startify_skiplist = [
      \ 'COMMIT_EDITMSG',
      \ $HOME . '/.bashrc',
      \ $HOME . '/.bash_aliases',
      \ $HOME . '/.config',
      \ $HOME . '/.dotfiles',
      \ $HOME . '/.Xresources',
      \ $HOME . '/.vimrc',
      \ $HOME . '/.zshrc',
      \ $HOME . '/.oh-my-zsh/custom/custom.zsh',
      \ ]

let g:startify_custom_header =
      \ map(split(system('figlet Vim 8 . 1'), '\n'), '"    ". v:val')
"}}}

"{{{ VSCodium
augroup ide
  autocmd!

  nnoremap <leader>vs :silent exec "!open vscodium://file/" . expand("%:p") . ":" . line(".") . ":" . col(".")<cr>:redraw!<cr>
augroup END
"}}}

"{{{ Markdown
augroup ft_text
  autocmd!

  autocmd BufNewFile,BufReadPost *.md set filetype=markdown

  autocmd FileType markdown setlocal wrap
  autocmd FileType markdown setlocal spell
  autocmd FileType markdown setlocal linebreak
  nnoremap <leader>mdo :w<cr>:silent exec "!killall MacDown"<cr>:silent exec "!open %"<cr>:redraw!<cr>
  nnoremap <leader>mdc :silent exec "!killall MacDown"<cr>:redraw!<cr>

  let g:markdown_fenced_languages = ['html', 'python', 'bash=sh']
augroup END
"}}}

"{{{ Text
augroup ft_shell
  autocmd!

  autocmd FileType sh setlocal nowrap
  autocmd FileType sh setlocal textwidth=0
  autocmd FileType sh setlocal nolinebreak
augroup END
"}}}

"{{{ Text
augroup ft_text
  autocmd!

  autocmd FileType text setlocal wrap
  autocmd FileType text setlocal spell
  autocmd FileType text setlocal linebreak
augroup END
"}}}

"{{{ Tmux
" Alt + cursor keys to mimic tmux behaviour
nnoremap <m-left> <c-w>h
nnoremap <m-right> <c-w>l
nnoremap <m-up> <c-w>k
nnoremap <m-down> <c-w>j
"}}}

"{{{ Vimrc
nnoremap <leader>ev :vsp $MYVIMRC<cr>
"}}}

"{{{ Vagrant
augroup ft_vagrant
  autocmd!
  autocmd BufRead,BufNewFile Vagrantfile set ft=ruby
augroup END
"}}}

"{{{ XML
function! DoPrettyXML()
  " save the filetype so we can restore it later
  let l:origft = &ft
  set ft=
  " delete the xml header if it exists. This will
  " permit us to surround the document with fake tags
  " without creating invalid xml.
  1s/<?xml .*?>//e
  " insert fake tags around the entire document.
  " This will permit us to pretty-format excerpts of
  " XML that may contain multiple top-level elements.
  0put ='<PrettyXML>'
  $put ='</PrettyXML>'
  silent %!xmllint --format -
  " xmllint will insert an <?xml?> header. it's easy enough to delete
  " if you don't want it.
  " delete the fake tags
  2d
  $d
  " restore the 'normal' indentation, which is one extra level
  " too deep due to the extra tags we wrapped around the document.
  silent %<
  " back to home
  1
  " restore the filetype
  exe "set ft=" . l:origft
  set filetype=xml
endfunction
command! PrettyXML call DoPrettyXML()
"}}}

"{{{ Yankring
let tmpdir = expand("~/.vim/tmp")
if !isdirectory(tmpdir)
  call mkdir(tmpdir)
endif
let g:yankring_history_dir=tmpdir

nnoremap <leader>y :YRShow<cr>
"}}}

let g:coc_node_path = "/Users/bensinclair/.nvm/versions/node/v8.16.2/bin/node"
let g:coc_global_extensions = [
  \ 'coc-prettier',
  \ 'coc-eslint',
  \ 'coc-json',
  \ 'coc-phpls',
  \ 'coc-python',
  \ ]

" Use tab for trigger completion with characters ahead and navigate.
" Use command ':verbose imap <tab>' to make sure tab is not mapped by other plugin.
inoremap <silent><expr> <TAB>
      \ pumvisible() ? "\<C-n>" :
      \ <SID>check_back_space() ? "\<TAB>" :
      \ coc#refresh()
inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<C-h>"

function! s:check_back_space() abort
  let col = col('.') - 1
  return !col || getline('.')[col - 1]  =~# '\s'
endfunction

" Use <c-space> to trigger completion.
inoremap <silent><expr> <c-space> coc#refresh()

" Use <cr> to confirm completion, `<C-g>u` means break undo chain at current position.
" Coc only does snippet and additional edit on confirm.
inoremap <expr> <cr> pumvisible() ? "\<C-y>" : "\<C-g>u\<CR>"
" Or use `complete_info` if your vim support it, like:
" inoremap <expr> <cr> complete_info()["selected"] != "-1" ? "\<C-y>" : "\<C-g>u\<CR>"

" Use `[g` and `]g` to navigate diagnostics
nmap <silent> [g <Plug>(coc-diagnostic-prev)
nmap <silent> ]g <Plug>(coc-diagnostic-next)

" Remap keys for gotos
nmap <silent> gd <Plug>(coc-definition)
nmap <silent> gy <Plug>(coc-type-definition)
nmap <silent> gi <Plug>(coc-implementation)
nmap <silent> gr <Plug>(coc-references)

" Use K to show documentation in preview window
nnoremap <silent> K :call <SID>show_documentation()<CR>

function! s:show_documentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'h '.expand('<cword>')
  else
    call CocAction('doHover')
  endif
endfunction

" Highlight symbol under cursor on CursorHold
" autocmd CursorHold * silent call CocActionAsync('highlight')

" Create mappings for function text object, requires document symbols feature of languageserver.
xmap if <Plug>(coc-funcobj-i)
xmap af <Plug>(coc-funcobj-a)
omap if <Plug>(coc-funcobj-i)
omap af <Plug>(coc-funcobj-a)
